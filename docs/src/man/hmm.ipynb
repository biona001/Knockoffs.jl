{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Generate HMM knockoffs\n",
    "\n",
    "This tutorial closely follows the [knockoffgwas tutorial](https://msesia.github.io/knockoffgwas/tutorial.html). We go over how to generate (SHAPEIT) HMM knockoffs given (simulated) binary PLINK formatted data. "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Installation\n",
    "\n",
    "1. Install [knockoffgwas](https://github.com/msesia/knockoffgwas) and its dependencies\n",
    "2. Install [qctools](https://www.well.ox.ac.uk/~gav/qctool_v2/documentation/download.html) for converting between VCF and BGEN formats\n",
    "3. Install [RaPID](https://github.com/ZhiGroup/RaPID) for detecting IBD segments\n",
    "3. Install the following Julia packages. Within julia, type\n",
    "```julia\n",
    "]add SnpArrays Distributions ProgressMeter MendelIHT VCFTools\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Required inputs\n",
    "\n",
    "We need multiple input files to generate knockoffs\n",
    "\n",
    "+ Unphased genotypes in binary PLINK format\n",
    "+ Phased genotypes in VCF and BGEN format: we will simulate haplotypes, store in VCF format, and convert to BGEN using [qctools](https://www.well.ox.ac.uk/~gav/qctool_v2/documentation/examples/converting.html)\n",
    "    + Note: knockoffgwas requires only BGEN format, but RaPID requires VCF formats. Hence, the extra conversion\n",
    "+ Map file (providing different group resolution): since data is simulated, we will generate fake map file where every snp is 1cM apart\n",
    "+ IBD segment file (generated by [RaPID](https://github.com/ZhiGroup/RaPID))\n",
    "+ Variant partition files (generated by snpknock2, i.e. module 2 of [knockoffgwas](https://github.com/msesia/knockoffgwas)) \n",
    "+ Sample and variant QC files"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Simulate genotypes\n",
    "\n",
    "\n",
    "Our simulation will try to follow\n",
    "+ Adding population structure (admixture & cryptic relatedness): [New approaches to population stratification in genome-wide association studies](https://www.nature.com/articles/nrg2813)\n",
    "+ How to simulate siblings: [Using Extended Genealogy to Estimate Components of Heritability for 23 Quantitative and Dichotomous Traits\n",
    "](https://journals.plos.org/plosgenetics/article?id=10.1371/journal.pgen.1003520)\n",
    "\n",
    "### Population structure\n",
    "\n",
    "Specifically, lets simulate genotypes with 2 populations. We simulate 49950 normally differentiated markers and 50 unusually differentiated markers based on allele frequency difference equal to 0.6. Let $x_{ij}$ be the number of alternate allele count for sample $i$ at SNP $j$ with allele frequency $p_j$. Also let $h_{ij, 1}$ denotype haplotype 1 of sample $i$ at SNP $j$ and $h_{ij, 2}$ the second haplotype. Our simulation model is\n",
    "\n",
    "$$h_{ij, 1} \\sim Bernoulli(p_j), \\quad h_{ij, 2} \\sim Bernoulli(p_j), \\quad x_{ij} = h_{ij, 1} + h_{ij, 2}$$\n",
    "\n",
    "which is equivalent to  \n",
    "\n",
    "$$x_{ij} \\sim Binomial(2, p_j)$$\n",
    "\n",
    "for unphased data. The allele frequency is $p_{j} = Uniform(0, 1)$ for normally differentiated markers, and \n",
    "\n",
    "$$p_{pop1, j} \\sim Uniform(0, 0.4), \\quad p_{pop2, j} = p_{pop1, j} + 0.6$$\n",
    "\n",
    "for abnormally differentiated markers. Each sample is randomly assigned to population 1 or 2. \n",
    "\n",
    "### Sibling pairs\n",
    "\n",
    "**Note: sibling pairs are not included yet, but will be easily added soon**."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 90,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "RaPID (generic function with 1 method)"
      ]
     },
     "execution_count": 90,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# load Julia packages and some helper functions\n",
    "using SnpArrays\n",
    "using DelimitedFiles\n",
    "using Random\n",
    "using LinearAlgebra\n",
    "using Distributions\n",
    "using ProgressMeter\n",
    "using MendelIHT\n",
    "using VCFTools\n",
    "\n",
    "\"\"\"\n",
    "    simulate_pop_structure(plinkfile, n, p)\n",
    "\n",
    "Simulate genotypes with K = 2 populations. 50 SNPs will have different allele \n",
    "frequencies between the populations. \n",
    "\n",
    "# Inputs\n",
    "- `plinkfile`: Output plink file name. \n",
    "- `n`: Number of samples\n",
    "- `p`: Number of SNPs\n",
    "\n",
    "# Output\n",
    "- `x`: A simulated `SnpArray`. Also saves binary plink files `plinkfile.bed`,\n",
    "    `plinkfile.bim`, `plinkfile.fam` to the current directory\n",
    "- `populations`: Vector of length `n` indicating population membership for each\n",
    "    sample. Also saved as `populations.txt`\n",
    "- `diff_markers`: Indices of the differentially expressed alleles. Also saved \n",
    "    as `diff_markers.txt`\n",
    "\"\"\"\n",
    "function simulate_pop_structure(n::Int, p::Int)\n",
    "    # first simulate genotypes treating all samples equally\n",
    "    x1 = BitMatrix(undef, n, p)\n",
    "    x2 = BitMatrix(undef, n, p)\n",
    "    pmeter = Progress(p, 0.1, \"Simulating genotypes...\")\n",
    "    @inbounds for j in 1:p\n",
    "        d = Bernoulli(rand())\n",
    "        for i in 1:n\n",
    "            x1[i, j] = rand(d)\n",
    "            x2[i, j] = rand(d)\n",
    "        end\n",
    "        next!(pmeter)\n",
    "    end\n",
    "    # assign populations and simulate 50 unually differentiated markers\n",
    "    populations = rand(1:2, n)\n",
    "    diff_markers = sample(1:p, 50, replace=false)\n",
    "    @inbounds for j in diff_markers\n",
    "        pop1_allele_freq = 0.4rand()\n",
    "        pop2_allele_freq = pop1_allele_freq + 0.6\n",
    "        pop1_dist = Bernoulli(pop1_allele_freq)\n",
    "        pop2_dist = Bernoulli(pop2_allele_freq)\n",
    "        for i in 1:n\n",
    "            d = isone(populations[i]) ? pop1_dist : pop2_dist\n",
    "            x1[i, j] = rand(d)\n",
    "            x2[i, j] = rand(d)\n",
    "        end\n",
    "    end\n",
    "    return x1, x2, populations, diff_markers\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Simplified VCF writer, taken from https://github.com/OpenMendel/MendelImpute.jl/blob/master/src/impute.jl#L23\n",
    "\"\"\"\n",
    "function write_vcf(outfile::AbstractString, x1::AbstractMatrix, x2::AbstractMatrix)\n",
    "    n, p = size(x1)\n",
    "    chr = [\"1\"     for i in 1:p]\n",
    "    pos = [100i    for i in 1:p]\n",
    "    ids = [\"snp$i\" for i in 1:p]\n",
    "    ref = [\"A\"     for i in 1:p]\n",
    "    alt = [\"T\"     for i in 1:p]\n",
    "    io = openvcf(outfile * \".vcf.gz\", \"w\")\n",
    "    pb = PipeBuffer()\n",
    "    \n",
    "    # minimum meta info\n",
    "    print(pb, \"##fileformat=VCFv4.2\\n\")\n",
    "    print(pb, \"##source=Knockoff.jl\\n\")\n",
    "    print(pb, \"##FORMAT=<ID=GT,Number=1,Type=String,Description=\\\"Genotype\\\">\\n\")\n",
    "\n",
    "    # header line (includes sample ID)\n",
    "    sampleID = [string(i) for i in 1:n]\n",
    "    print(pb, \"#CHROM\\tPOS\\tID\\tREF\\tALT\\tQUAL\\tFILTER\\tINFO\\tFORMAT\")\n",
    "    for id in sampleID\n",
    "        print(pb, \"\\t\", id)\n",
    "    end\n",
    "    print(pb, \"\\n\")\n",
    "    bytesavailable(pb) > 1048576 && write(io, take!(pb))\n",
    "    \n",
    "    # loop over genotypes\n",
    "    pmeter = Progress(p, 1, \"Writing VCF...\")\n",
    "    @inbounds for i in 1:p\n",
    "        # write meta info (chrom/pos/snpid/ref/alt/imputation-quality)\n",
    "        print(pb, chr[i], \"\\t\", string(pos[i]), \"\\t\", ids[i], \"\\t\", \n",
    "            ref[i], \"\\t\", alt[i], \"\\t.\\tPASS\\t.\\tGT\")\n",
    "        # print ith record\n",
    "        write_snp!(pb, x1, x2, i) \n",
    "        bytesavailable(pb) > 1048576 && write(io, take!(pb))\n",
    "        next!(pmeter)\n",
    "    end\n",
    "    write(io, take!(pb))\n",
    "    close(io); close(pb) # close io and buffer\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Helper function for saving the `i`th record (SNP), tracking phase information.\n",
    "Here `X = X1 + X2`.\n",
    "\"\"\"\n",
    "function write_snp!(pb::IOBuffer, X1::AbstractMatrix, X2::AbstractMatrix, i::Int)\n",
    "    x1 = @view(X1[:, i])\n",
    "    x2 = @view(X2[:, i])\n",
    "    n = length(x1)\n",
    "    @assert n == length(x2)\n",
    "    @inbounds for j in 1:n\n",
    "        if x1[j] == x2[j] == 0\n",
    "            print(pb, \"\\t0|0\")\n",
    "        elseif x1[j] == 0 && x2[j] == 1\n",
    "            print(pb, \"\\t0|1\")\n",
    "        elseif x1[j] == 1 && x2[j] == 0\n",
    "            print(pb, \"\\t1|0\")\n",
    "        elseif x1[j] == 1 && x2[j] == 1\n",
    "            print(pb, \"\\t1|1\")\n",
    "        else\n",
    "            error(\"phased genotypes can only be 0|0, 0|1, 1|0 or 1|1 but\n",
    "                got $(x1[j])|$(x2[j])\")\n",
    "        end\n",
    "    end\n",
    "    print(pb, \"\\n\")\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function write_plink(outfile::AbstractString, x1::AbstractMatrix, x2::AbstractMatrix)\n",
    "    n, p = size(x1)\n",
    "    x = SnpArray(outfile * \".bed\", n, p)\n",
    "    for j in 1:p, i in 1:n\n",
    "        c = x1[i, j] + x2[i, j]\n",
    "        if c == 0\n",
    "            x[i, j] = 0x00\n",
    "        elseif c == 1\n",
    "            x[i, j] = 0x02\n",
    "        elseif c == 2\n",
    "            x[i, j] = 0x03\n",
    "        else\n",
    "            error(\"matrix entries should be 0, 1, or 2 but was $c!\")\n",
    "        end\n",
    "    end\n",
    "    # create .bim file structure: https://www.cog-genomics.org/plink2/formats#bim\n",
    "    open(outfile * \".bim\", \"w\") do f\n",
    "        for i in 1:p\n",
    "            println(f, \"1\\tsnp$i\\t0\\t$(100i)\\t1\\t2\")\n",
    "        end\n",
    "    end\n",
    "    # create .fam file structure: https://www.cog-genomics.org/plink2/formats#fam\n",
    "    open(outfile * \".fam\", \"w\") do f\n",
    "        for i in 1:n\n",
    "            println(f, \"$i\\t1\\t0\\t0\\t1\\t-9\")\n",
    "        end\n",
    "    end\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function make_fake_mapfile(filename, p::Int)\n",
    "    open(filename, \"w\") do io\n",
    "        println(io, \"Chromosome\\tPosition(bp)\\tRate(cM/Mb)\\tMap(cM)\")\n",
    "        for i in 1:p\n",
    "            println(io, \"chr1\\t\", 100i, \"\\t1.0\\t1.0\")\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function partition(plinkfile, rscriptPath, mapfile, qc_variants, outfile)\n",
    "    bimfile = plinkfile * \".bim\"\n",
    "    run(`Rscript --vanilla $rscriptPath $mapfile $bimfile $qc_variants $outfile`)\n",
    "end\n",
    "\n",
    "function RaPID(executable_path, vcffile, mapfile, min_length, outfolder, window_size, r, s)\n",
    "    isdir(outfolder) || mkdir(outfolder)\n",
    "    run(`$executable_path -i $vcffile -g $mapfile -d $min_length -o $outfolder -w $window_size -r $r -s $s`)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Simulate phased and unphased data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 75,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[32mSimulating genotypes...100%|████████████████████████████| Time: 0:00:00\u001b[39m\n",
      "\u001b[32mWriting VCF...100%|█████████████████████████████████████| Time: 0:00:19\u001b[39m\n"
     ]
    }
   ],
   "source": [
    "# simulate\n",
    "Random.seed!(2021)\n",
    "outfile = \"sim\"\n",
    "n = 2000\n",
    "p = 50000\n",
    "x1, x2, populations, diff_markers = simulate_pop_structure(n, p)\n",
    "\n",
    "# write phased genotypes to VCF format\n",
    "write_vcf(outfile * \".phased\", x1, x2)\n",
    "\n",
    "# write unphased genotypes to PLINK binary format\n",
    "write_plink(outfile, x1, x2)\n",
    "\n",
    "# save pop1/pop2 index and unually differentiated marker indices\n",
    "writedlm(\"populations.txt\", populations)\n",
    "writedlm(\"diff_markers.txt\", diff_markers)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 76,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "diff_markers.txt\n",
      "hmm.ipynb\n",
      "populations.txt\n",
      "qc_variants.txt\n",
      "sim\n",
      "sim.bed\n",
      "sim.bim\n",
      "sim.fam\n",
      "sim.map\n",
      "sim.partition.txt\n",
      "sim.phased.vcf.gz\n"
     ]
    }
   ],
   "source": [
    ";ls"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Step 1: Partitions\n",
    "\n",
    "We need\n",
    "+ Map file (in particular the (cM) field will determine group resolution)\n",
    "+ PLINK's bim file\n",
    "+ QC file (all SNP names that pass QC)\n",
    "+ output file name\n",
    "\n",
    "Since data is simulated, there are no genomic map file. Let us generate a fake one. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 79,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# generate fake map file\n",
    "make_fake_mapfile(\"sim.map\", p)\n",
    "\n",
    "# also generate QC file that contains all SNPs and all samples\n",
    "snpdata = SnpData(\"sim\")\n",
    "snpIDs = snpdata.snp_info[!, :snpid]\n",
    "sampleIDs = Matrix(snpdata.person_info[!, 1:2])\n",
    "writedlm(\"variants_qc.txt\", snpIDs)\n",
    "writedlm(\"samples_qc.txt\", sampleIDs)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now we run the partition script"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 83,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "Warning messages:\n",
      "1: Quick-TRANSfer stage steps exceeded maximum (= 2500000) \n",
      "2: Quick-TRANSfer stage steps exceeded maximum (= 2500000) \n",
      "3: Quick-TRANSfer stage steps exceeded maximum (= 2500000) \n",
      "4: Quick-TRANSfer stage steps exceeded maximum (= 2500000) \n",
      "5: Quick-TRANSfer stage steps exceeded maximum (= 2500000) \n"
     ]
    },
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Mean group sizes: \n",
      "res_7 res_6 res_5 res_4 res_3 res_2 res_1 \n",
      "    1 10000 10000 10000 10000 10000 10000 \n",
      "Partitions written to: sim.partition.txt\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "Process(`\u001b[4mRscript\u001b[24m \u001b[4m--vanilla\u001b[24m \u001b[4m/Users/biona001/Desktop/knockoffgwas/knockoffgwas/utils/partition.R\u001b[24m \u001b[4msim.map\u001b[24m \u001b[4msim.bim\u001b[24m \u001b[4mvariants_qc.txt\u001b[24m \u001b[4msim.partition.txt\u001b[24m`, ProcessExited(0))"
      ]
     },
     "execution_count": 83,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "plinkfile = \"sim\"\n",
    "rscriptPath = \"/scratch/users/bbchu/knockoffgwas/knockoffgwas/utils/partition.R\"\n",
    "# rscriptPath = \"/Users/biona001/Desktop/knockoffgwas/knockoffgwas/utils/partition.R\"\n",
    "mapfile = \"sim.map\"\n",
    "qc_variants = \"variants_qc.txt\"\n",
    "outfile = \"sim.partition.txt\"\n",
    "partition(plinkfile, rscriptPath, mapfile, qc_variants, outfile)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Step 2: Generate Knockoffs\n",
    "\n",
    "We need:\n",
    "1. Phased genotypes in VCF and BGEN format: we will simulate haplotypes, store in VCF format, and convert to BGEN using qctools\n",
    "    + Note: knockoffgwas requires only BGEN format, but RaPID requires VCF formats. Hence, the extra conversion\n",
    "+ IBD segment file generated by RaPID where input is VCF format (**this seems to only run on linux**)\n",
    "\n",
    "First generate IBD segment files"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 93,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "/Users/biona001/Desktop/RaPID/RaPID_v.1.7: /Users/biona001/Desktop/RaPID/RaPID_v.1.7: cannot execute binary file\n"
     ]
    },
    {
     "ename": "LoadError",
     "evalue": "failed process: Process(`\u001b[4m/Users/biona001/Desktop/RaPID/RaPID_v.1.7\u001b[24m \u001b[4m-i\u001b[24m \u001b[4msim.phased.vcf.gz\u001b[24m \u001b[4m-g\u001b[24m \u001b[4msim.map\u001b[24m \u001b[4m-d\u001b[24m \u001b[4m5\u001b[24m \u001b[4m-o\u001b[24m \u001b[4mrapid\u001b[24m \u001b[4m-w\u001b[24m \u001b[4m250\u001b[24m \u001b[4m-r\u001b[24m \u001b[4m10\u001b[24m \u001b[4m-s\u001b[24m \u001b[4m2\u001b[24m`, ProcessExited(126)) [126]\n",
     "output_type": "error",
     "traceback": [
      "failed process: Process(`\u001b[4m/Users/biona001/Desktop/RaPID/RaPID_v.1.7\u001b[24m \u001b[4m-i\u001b[24m \u001b[4msim.phased.vcf.gz\u001b[24m \u001b[4m-g\u001b[24m \u001b[4msim.map\u001b[24m \u001b[4m-d\u001b[24m \u001b[4m5\u001b[24m \u001b[4m-o\u001b[24m \u001b[4mrapid\u001b[24m \u001b[4m-w\u001b[24m \u001b[4m250\u001b[24m \u001b[4m-r\u001b[24m \u001b[4m10\u001b[24m \u001b[4m-s\u001b[24m \u001b[4m2\u001b[24m`, ProcessExited(126)) [126]\n",
      "",
      "Stacktrace:",
      " [1] pipeline_error",
      "   @ ./process.jl:525 [inlined]",
      " [2] run(::Cmd; wait::Bool)",
      "   @ Base ./process.jl:440",
      " [3] run",
      "   @ ./process.jl:438 [inlined]",
      " [4] RaPID(executable_path::String, vcffile::String, mapfile::String, min_length::Int64, outfolder::String, window_size::Int64, r::Int64, s::Int64)",
      "   @ Main ./In[90]:175",
      " [5] top-level scope",
      "   @ In[93]:10",
      " [6] eval",
      "   @ ./boot.jl:360 [inlined]",
      " [7] include_string(mapexpr::typeof(REPL.softscope), mod::Module, code::String, filename::String)",
      "   @ Base ./loading.jl:1094"
     ]
    }
   ],
   "source": [
    "# executable = \"/scratch/users/bbchu/RaPID/RaPID_v.1.7\"\n",
    "executable = \"/Users/biona001/Desktop/RaPID/RaPID_v.1.7\"\n",
    "vcffile = \"sim.phased.vcf.gz\"\n",
    "mapfile = \"sim.map\"\n",
    "min_length = 5\n",
    "outfolder = \"rapid\"\n",
    "window_size = 250\n",
    "r = 10\n",
    "s = 2\n",
    "RaPID(executable, vcffile, mapfile, min_length, outfolder, window_size, r, s)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Run the following code in the command line directly. You may need to adjust file directories and change parameters. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "# Path to snpknock2 executable built as described above\n",
    "SNPKNOCK2=\"/scratch/users/bbchu/knockoffgwas/snpknock2/bin/snpknock2\"\n",
    "\n",
    "# Create directory for output\n",
    "mkdir -p \"./tmp/knockoffs\"\n",
    "\n",
    "# Run snpknock2\n",
    "$SNPKNOCK2 \\\n",
    "  --bgen \"./data/haplotypes/example_chr{21:22}\" \\\n",
    "  --keep \"./samples_qc.txt\" \\\n",
    "  --extract \"./data/qc/qc_chr{21:22}.txt\" \\\n",
    "  --map \"./data/maps/genetic_map_chr{21:22}.txt\" \\\n",
    "  --part \"./data/partitions/example_chr{21:22}.txt\" \\\n",
    "  --ibd \"./data/ibd/example_chr{21:22}.txt\" \\\n",
    "  --K 10 \\\n",
    "  --cluster_size_min 1000 \\\n",
    "  --cluster_size_max 10000 \\\n",
    "  --hmm-rho 1 \\\n",
    "  --hmm-lambda 1e-3 \\\n",
    "  --windows 0 \\\n",
    "  --n_threads 10 \\\n",
    "  --seed 2020 \\\n",
    "  --compute-references \\\n",
    "  --generate-knockoffs \\\n",
    "  --out \"./tmp/knockoffs/knockoffs_chr{21:22}\""
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.0",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
